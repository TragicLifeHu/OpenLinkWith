plugins {
  id 'com.android.application'
  id 'com.novoda.build-properties'
}

apply from: configFile('android-common-config.gradle')

buildProperties {
  signing.using(configFile('release.signing')) | configFile('debug.signing')
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
  defaultConfig {
    applicationId 'com.tasomaniac.openwith'
    versionCode versions.versionCode
    versionName versions.versionName
    buildConfigString 'GIT_SHA', gitSha
    resourceConfigurations += ['ar', 'de', 'en', 'es', 'fi', 'fr', 'hi', 'hr', 'in', 'it', 'iw', 'ja', 'ko', 'nl', 'pl', 'pt-rBR', 'pt-rPT', 'ru', 'sv', 'tr', 'uk', 'vi', 'zh-rCN', 'zh-rTW']
  }

  signingConfigs {
    debug.storeFile configFile('debug.keystore')
    release {
      def signing = buildProperties.signing
      storeFile configFile(signing['storeFile'].string)
      storePassword signing['storePassword'].string
      keyAlias signing['keyAlias'].string
      keyPassword signing['keyPassword'].string
    }
  }

  buildTypes {
    debug {
      ext.enableCrashlytics = false
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  flavorDimensions += ['store']
  productFlavors {
    floss {
      dimension "store"
      applicationIdSuffix '.floss'
      versionNameSuffix '-floss'
    }
    play {
      dimension "store"
    }
  }
  namespace 'com.tasomaniac.openwith'
  buildFeatures {
    buildConfig true
    viewBinding true
  }
}

if (isPlay) {
  apply from: configFile('configure-crashlytics.gradle')
  apply from: configFile('play-publish.gradle')
}

configurations {
  implementation.exclude module: 'support-v4'
  compile.exclude module: 'support-v4'
}

dependencies {
  implementation project(':base-prefs')
  implementation project(':browser-preferred')
  implementation project(':browser-resolver')
  implementation project(':data')
  implementation project(':homescreen')
  implementation project(':intro')
  implementation project(':preferred-apps')
  implementation project(':redirect')
  implementation project(':resolver')

  implementation 'androidx.preference:preference-ktx:1.2.1'
  implementation 'androidx.sharetarget:sharetarget:1.2.0'

  implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"

  testImplementation project(':test-support')
}
